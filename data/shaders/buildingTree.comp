#version 430 core

struct TreeNode_t 
{
	vec4 centerOfMass;
	float totalMass;
	ivec2 children;
	float boundingBoxMetric;
};

layout (local_size_x = 8, local_size_y = 4, local_size_z = 1) in;

layout(std430, binding = 1) buffer posInputBuffer
{
    vec4 posInputSSBO[];
};
layout(std430, binding = 3) buffer velInputBuffer
{
    vec4 velInputSSBO[];
};  
layout(std430, binding = 5) buffer mortonCodeBuffer
{
    uint mortonCodesSSBO[];
};
layout(std430, binding = 6) buffer treeNodesBuffer
{
    TreeNode_t treeNodesSSBO[];
};

uniform int u_NumberOfParticlesSqrt;

float calculateBoundingBox(int i, int j)
{
	int left = min(i, j);
	int right = max(i, j);

	float maximumX = posInputSSBO[left].x;
	float minimumX = posInputSSBO[left].x;
	float maximumY = posInputSSBO[left].y;
	float minimumY = posInputSSBO[left].y;
	float maximumZ = posInputSSBO[left].z;
	float minimumZ = posInputSSBO[left].z;
	
	while (left < right) 
	{
		maximumX = max(maximumX, posInputSSBO[left + 1].x);
		minimumX = min(minimumX, posInputSSBO[left + 1].x);
		maximumY = max(maximumY, posInputSSBO[left + 1].y);
		minimumY = min(minimumY, posInputSSBO[left + 1].y);
		maximumZ = max(maximumZ, posInputSSBO[left + 1].z);
		minimumZ = min(minimumZ, posInputSSBO[left + 1].z);
		++left;
	}
	return max(max(maximumX - minimumX, maximumY - minimumY), maximumZ - minimumZ);
}

vec4 findCenterOfMass(int i, int j)
{
	vec4 centerOfMass = vec4(0, 0, 0, 0);
	int left = min(i, j);
	int right = max(i, j);
	int numberOfChildren = right - left + 1;
	int totalMass = numberOfChildren * 30000;
	for (int i = 0; i<numberOfChildren; ++i) {
		centerOfMass.x += (30000 * posInputSSBO[left + i].x)/totalMass;
		centerOfMass.y += (30000 * posInputSSBO[left + i].y)/totalMass;
		centerOfMass.z += (30000 * posInputSSBO[left + i].z)/totalMass;
	}
	return centerOfMass;
};

int sharedPrefixLength(int i, int j)
{
	if(j < 0 || j >= u_NumberOfParticlesSqrt * u_NumberOfParticlesSqrt) return -1;
	uint mi = mortonCodesSSBO[i];
	uint mj = mortonCodesSSBO[j];

	if(mi!=mj)
	{
		uint x = mi ^ mj;
		float f = log2(float(x) + 0.5);
		return 32 - int(f);
	}
	else
	{
		int x = i ^ j;
		float f = log2(float(x) + 0.5);
		return 64 - int(f);
	}
}

void main()
{
	ivec2 texelCoord = ivec2(gl_GlobalInvocationID.xy);
	int i = u_NumberOfParticlesSqrt * texelCoord.x + texelCoord.y;
	int direction = sign(sharedPrefixLength(i, i + 1) - sharedPrefixLength(i, i - 1));

	int minPrefixLength =  sharedPrefixLength(i, i - direction);
	int upperBound = 2;

	while(sharedPrefixLength(i, i + upperBound*direction) > minPrefixLength)
	{
		upperBound *= 2;
	}

	int l = 0;
    int t = upperBound/2;

    for(int k = 0; k<log2(u_NumberOfParticlesSqrt)+2; k++){
        if (sharedPrefixLength(i, i + (l + t) * direction) > minPrefixLength)
		{
            l = l+t;
        }
        if(t==1) break;
        t /= 2;
    }

	int j = i + l * direction;

	int dNode = sharedPrefixLength(i, j);
	int splitPos = 0;
	float ft = float(l)/2.;
    for(int k = 0; k<log2(u_NumberOfParticlesSqrt)+2; k++){
        t = int(max(1.,ceil(ft)));
        if(sharedPrefixLength(i, i + (splitPos + t) * direction ) > dNode){
            splitPos += t;
        }
		if(t==1) break;
        ft /= 2.;
    }
	int biggestLeftChild = i + splitPos * direction + min(0, direction);
	int leftChildIndex;
	int rightChildIndex;
	if (min(i, j) == biggestLeftChild) {
		leftChildIndex = biggestLeftChild;
		treeNodesSSBO[leftChildIndex] = TreeNode_t(posInputSSBO[leftChildIndex], 30000, ivec2(-1, -1), 1.0);
	}
	else {
		leftChildIndex = biggestLeftChild + u_NumberOfParticlesSqrt * u_NumberOfParticlesSqrt;
	}
	if (max(i, j) == biggestLeftChild + 1) {
		rightChildIndex = biggestLeftChild + 1;
		treeNodesSSBO[rightChildIndex] = TreeNode_t(posInputSSBO[rightChildIndex], 30000, ivec2(-1, -1), 1.0);
	}
	else {
		rightChildIndex = biggestLeftChild + 1 + u_NumberOfParticlesSqrt * u_NumberOfParticlesSqrt;
	}
	vec4 centerMass = findCenterOfMass(i, j);
	float boundingBox = calculateBoundingBox(i, j);
	treeNodesSSBO[u_NumberOfParticlesSqrt * u_NumberOfParticlesSqrt + i] = TreeNode_t(centerMass, l*3000, ivec2(leftChildIndex, rightChildIndex), boundingBox);
}
