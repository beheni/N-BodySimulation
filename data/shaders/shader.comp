#version 430 core
layout (local_size_x = 8, local_size_y = 4, local_size_z = 1) in;

layout(std430, binding = 1) buffer posInputBuffer
{
    vec4 positionInputSSBO[];
};            
layout(std430, binding = 2) buffer posOutputBuffer
{
    vec4 positionOutputSSBO[];
};             
layout(std430, binding = 3) buffer velInputBuffer
{
    vec4 velInputSSBO[];
};             
layout(std430, binding = 4) buffer velOutputBuffer
{
    vec4 velOutputSSBO[];
}; 

uniform float u_DeltaTime;
uniform int u_NumberOfParticlesSqrt;
uniform vec3 u_BoundingBox;

float G = 0.0001;

void main() 
{
    ivec2 texelCoord = ivec2(gl_GlobalInvocationID.xy);
  
    vec4 velocity = velInputSSBO[u_NumberOfParticlesSqrt * texelCoord.x + texelCoord.y];
    vec4 position = positionInputSSBO[u_NumberOfParticlesSqrt * texelCoord.x + texelCoord.y];
    // change velocity
    vec4 f = vec4(0, 0, 0, 0);

    for(uint i = 0; i < u_NumberOfParticlesSqrt; ++i)
    {
        for(uint j = 0; j < u_NumberOfParticlesSqrt; ++j)
        {
            ivec2 curTexel = ivec2(i, j);
            vec4 curPos = positionInputSSBO[u_NumberOfParticlesSqrt * curTexel.x + curTexel.y];

            vec4 pathVec = curPos - position;
            float dist = length(pathVec) + 1;
            float invDistCub = 1.0 / (dist * dist * dist);

            pathVec = pathVec * invDistCub;

            f = f + pathVec;
        }
    }

    f = f * G;
    velocity = velocity + f;
    position = position + velocity * u_DeltaTime;  

    if(abs(position.x) > u_BoundingBox.x || 
       abs(position.y) > u_BoundingBox.y ||
       abs(position.z) > u_BoundingBox.z)
    {
        //position = vec4(0, 0, 0, 0);
        velocity = velocity * vec4(-1,-1,-1,-1);
        position = position + velocity * u_DeltaTime;  
    }

    velOutputSSBO[u_NumberOfParticlesSqrt * texelCoord.x + texelCoord.y] = velocity;
    positionOutputSSBO[u_NumberOfParticlesSqrt * texelCoord.x + texelCoord.y] = position;
}